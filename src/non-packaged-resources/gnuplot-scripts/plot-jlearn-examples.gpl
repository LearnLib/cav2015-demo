# This Gnuplot script plots a bar chart for a performance comparison
# between selected algorithms from LearnLib and JLearn on a *single* example.
# For each <name>.dat file in the current directory, a file <name>-time.svg
# is created, plotting the runtimes, and <name>-queries.svg, plotting the
# number of required membership queries.

set xtics ("L*" 0.25, "MP" 1.25, "RS" 2.25, "DHC" 3.25, "DT" 4.25)

set boxwidth 0.4
set style fill solid

set ylabel 'Time (ms)'
set grid ytics lt 0 lw 1 lc rgb "#bbbbbb"

# set key off
set yrange [0:*]
set xrange [-0.5:5]
set offset 0, 0, graph 0.2, 0
set terminal svg size 640,400 fname 'Verdana' fsize 14
list=system('ls -1B libalf-examples-*.dat')

do for [f in list] {
	n=f[:strlen(f)-4]
	set ylabel 'Time (s)'
	set output n.'-time.svg'
	plot n.'.dat' using 2:($3/1000) with boxes ls 3 title 'LearnLib',\
		'' using ($2+0.4):($5/1000) with boxes ls 1 title 'JLearn'
	set ylabel 'Queries'
	set output n.'-queries.svg'
	plot n.'.dat' using 2:4 with boxes ls 3 title 'LearnLib',\
		'' using ($2+0.4):6 with boxes ls 1 title 'JLearn'
}
